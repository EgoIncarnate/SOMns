class TestRunner usingPlatform: platform = (
| private platform = platform.
  private system   = platform system.
  private actors   = platform actors.
  private minitest = (system loadModule: 'core-lib/TestSuite/Minitest.som') usingPlatform: platform.
|
)(

  printNameAndDescription: failureOrError = (
    failureOrError testCase environment classDeclarationMirror name asString print.
      '>>#' print. failureOrError testCase selector println.
    '\t' print. failureOrError description println.
  )
  
  reportErrors: errors = (
    errors size > 0 ifTrue: [
      'Errors' println.
      '------\n' println.
  
      errors do: [:terr |
        printNameAndDescription: terr.
      ].

      '' println.
    ].
  )
  
  reportFailures: failures = (
    failures size > 0 ifTrue: [
      'Failures' println.
      '--------\n' println.
  
      failures do: [:failure |
        printNameAndDescription: failure
      ].

      '' println.
    ].
  )
  
  runAllTests: testModule = (
    | tester catalog promisePair |
    catalog := minitest TestCatalog forModule: testModule.
    tester := minitest Tester testSuite: catalog allTests.
    
    promisePair := actors createPromisePair.

    tester runAll whenResolved: [:aTester |
      reportErrors:   tester errors.
      reportFailures: tester failures.

      'Total Number of Tests:      ' print.
      (tester errors size + tester failures size + tester successes size) println.
      'Number of Successful Tests: ' print.
      tester successes size println.
      
      promisePair resolver resolve: tester errors size + tester failures size = 0
    ].
    ^ promisePair promise
  )
  
  runAllKnownModules = (
    | modules allSuccessful allTestsDonePromise |
    modules := 'LanguageTests',
               'CollectionTests',
               'DoubleTests',
               'IntegerTests',
               'StringTests',
               'SymbolTests',
               'SystemTests',
               'BenchmarkHarnessTests',
               'MinitestTests'.
               
               (* 'ReflectionTests', *)
    
    allSuccessful := true.
    allTestsDonePromise := actors async: modules do: [:m |
      | testModule |
      testModule := (system loadModule: 'core-lib/TestSuite/' + m + '.som')
                      usingPlatform: platform testFramework: minitest.
      m println.
      (runAllTests: testModule)
        whenResolved: [:successful |
        allSuccessful := allSuccessful and: successful.
        '' println.
      ]
    ].
    ^ allTestsDonePromise whenResolved: [:ms | allSuccessful ]
  )

  public main: args = (
    | testModule |
    args size = 2 ifTrue: [
      | fut |
      testModule := (system loadModule: (args at: 2)) usingPlatform: platform testFramework: minitest.
      fut := runAllTests: testModule.
      fut whenResolved: [:testsPassed | 
        testsPassed
          ifTrue:  [ system exit: 0 ]
          ifFalse: [ system exit: 1 ] ]
    ].
    
    args size = 1 ifTrue: [
      runAllKnownModules
        whenResolved: [:testsPassed | 
          testsPassed
            ifTrue:  [ system exit: 0 ]
            ifFalse: [ system exit: 1 ] ]
    ].

    'TODO: add help text' println.
    ^ 1
  )
)

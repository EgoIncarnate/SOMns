class ClassStructureTests usingPlatform: platform testFramework: minitest = (
| private TestContext  = minitest TestContext.
  private ObjectMirror = platform mirrors ObjectMirror. |
)(
  public class ClassA = (
    | a b |
  ) (
    result = ( ^ 42 )
  )

  public class ClassB = ClassA (
    | c d |
  ) ( )
  
  public class ClassC = ClassB (
    | e f |
  ) (
    a      = ( ^ a )
    a: val = ( a := val )
  
    f      = ( ^ f )
    f: val = ( f := val )
  )

  public class ClassStructureTest = TestContext ()(

    public testClassIdentity = (
      self assert: #Array   equals: (ObjectMirror reflecting: Array new) className.
      self assert: #Integer equals: (ObjectMirror reflecting: 1) className.
      self assert: #Integer equals: (ObjectMirror reflecting: 10000000000) className.
      self assert: #Double  equals: (ObjectMirror reflecting: (1 // 2)) className.
      self assert: #Double  equals: (ObjectMirror reflecting: 0.5) className.
      self assert: #Block1  equals: (ObjectMirror reflecting: [42]) className.
      self assert: #Object  equals: (ObjectMirror reflecting: Object new) className.
      self assert: #Set     equals: (ObjectMirror reflecting: Set new) className.
      self assert: #String  equals: (ObjectMirror reflecting: 'foo') className.
      self assert: #Symbol  equals: (ObjectMirror reflecting: #foo) className.
      self assert: #True    equals: (ObjectMirror reflecting: true) className.
      self assert: #False   equals: (ObjectMirror reflecting: false) className.
      self assert: #Nil     equals: (ObjectMirror reflecting: nil) className.
    )
        
    public testAccessToInstanceFields = (
      | o |
      o := ClassC new.
      o a: 333.
      self assert: 333 equals: o a.
    
      o f: 333.
      self assert: 333 equals: o f.
    )
  
    public testMetaclasses = (
      self assert: nil              is: Object superclass.
      self assert: Integer          is: 1 class.
      self assert: #'Integer class' is: 1 class class name.
      self assert: Metaclass        is: 1 class class class.
    
      self assert: #'Metaclass class' is: Metaclass class name.
      self assert: Metaclass          is: Metaclass class class.
    
      self assert: Object          is: 1 class superclass.
      self assert: #'Object class' is: 1 class class superclass name.
      self assert: Class           is: Object class superclass.
      self assert: Metaclass       is: Class class class.
    )
  
    testInstanceFields = (
      self assert: 2 equals: ClassA fields length.
      self assert: 4 equals: ClassB fields length.
      self assert: 6 equals: ClassC fields length.
    )
  ) : ( TEST_CONTEXT = () )
)
